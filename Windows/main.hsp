#include "obj.as"
#include "inputbox.as"
#include "hspinet.as"
#include "user32.as"
#include "winmm.as"
#include "mod_menu.as"
#include "mod_WebView2_10115038.hsp"
#include "mod_regexp.as"
#define ChocoMintVersion "0.6"
#define LBN_DBLCLK	2

#define WM_SIZE				$00000005
#define WS_CHILD			$40000000
#define WS_VISIBLE			$10000000
#define WS_OVERLAPPEDWINDOW		$00CF0000

#packopt hide 1

screen 0,ginfo(20),ginfo(21),2,,,800,600
GetWindowLong hwnd,-16
SetWindowLong hwnd,-16,stat|$10000|$40000
gsel 0,1
title "ChocoMint"
onexit gosub *ONEXIT_
netinit
// WM_COMMAND_ を捕まえる
oncmd gosub *lbl_WM_COMMAND, WM_COMMAND
// ファイルメニュー作成
newmenu hfilemenu, 1
addmenu hfilemenu, "新規板登録(&A)", 2
addmenu hfilemenu, "板メニューをテキストファイルとして開く(&M)", 3
addmenu hfilemenu, "書き込みログ(&K)", 4
addmenu hfilemenu, "", 0,0x0800
addmenu hfilemenu, "終了(&Q)", 1
// ＢＢＳメニュー作成
newmenu hbbsmenu, 1
addmenu hbbsmenu, "更新チェック(&N)", 5
addmenu hbbsmenu, "新規スレッド作成(&N)", 6
addmenu hbbsmenu, "ローカルルール・看板表示(&L)", 7
// スレッドメニュー作成
newmenu hthreadmenu, 1
addmenu hthreadmenu, "更新チェック(&N)", 8
// トップメニュー作成
newmenu hmenu, 0
addmenu hmenu, "ファイル(&F)", hfilemenu, 0x10
addmenu hmenu, "ＢＢＳ(&B)", hbbsmenu, 0x10
addmenu hmenu, "スレッド(&T)", hthreadmenu, 0x10

applymenu hmenu

// リスト
temp=""
lists=""
exist "bbs.txt"
if strsize=-1:notesel temp:notesave "bbs.txt"
notesel bbs
noteload "bbs.txt"
repeat notemax
noteget temp,cnt
split temp,",",temp2
lists+=temp2(0)+"("+temp2(1)+")\n"
loop
// コントロール作成
objsize 120
pos 0,0
listbox select, ginfo_winy-24, lists
bbsmenus=stat
hListbox = objinfo(bbsmenus, 2)
sendmsg hListbox,$197,notemax ;LB_SETTOPINDEX

url=""
pos 120,0
input url,ginfo_winx-(120+80),20
pos ginfo_winx-80,0
objsize 80,20
button "→",*urlrun

subject=""
objsize ginfo_winx-120,0.1953125*ginfo_winy
pos 120,20
listbox select2, 0.1953125*ginfo_winy, subject
subjects=stat
hListbox2 = objinfo(subjects, 2)

message="":name="":mail=""
pos 120,0.5859375*ginfo_winy
mes "名前：",1:input name,130,19
pos 300,0.5859375*ginfo_winy
mes "E-mail（省略可）：",1:input mail,130,19
pos 575,0.5859375*ginfo_winy
objsize 60,40
button gosub "書き込み",*submit
pos 120
mesbox message,ginfo_winx-120,ginfo_winy-ginfo_cy-0.0390625*ginfo_winy,1
	
oncmd gosub *resize,0x5	//WM_SIZE。ウィンドウサイズ変更時にここにジャンプする。

pEnv = WebView2_CreateEnv()
pCtrl = WebView2_CreateCtrl(pEnv, hwnd)
WebView2_Size pCtrl, 120, 170, ginfo_winx, 0.5859375*ginfo_winy
pView = WebView2_GetView(pCtrl)

neturl "https://raw.githubusercontent.com/nennneko5787/ChocoMint/main/"
netrequest_get "WindowsVersion.txt"
;	結果待ちのためのループ
repeat
netexec res
if res : break
await
loop
netgetv versionCheck
if versionCheck != ChocoMintVersion{
winobj "msctls_statusbar32",  "お使いのChocoMintは最新ではありません。アップデートをお願いします。https://github.com/nennneko5787/ChocoMint/releases/latest/", , WS_CHILD | WS_VISIBLE
hStatus = objinfo(stat, 2)
}else{
;ステータスバー作成
winobj "msctls_statusbar32",  "お使いのChocoMintは最新です", , WS_CHILD | WS_VISIBLE
hStatus = objinfo(stat, 2)
}
stop

*lbl_WM_COMMAND
if (lparam == hListbox){
	hiword = (wparam >>16 )& 0xFFFF
	if (hiword == LBN_DBLCLK){
		urlrunv=0
		gosub *subjectload
	}
}

if (lparam == hListbox2){
	hiword = (wparam >>16 )& 0xFFFF
	if (hiword == LBN_DBLCLK){
		urlrunv=0
		gosub *threadload
	}
}

cmd = wparam & 0xFFFF
if cmd == 1 : end
if cmd == 2 {
	InputBox "板の名称を入力してください","ChocoMint",""
	bbsname=refstr
	if bbsname="":return
	InputBox "板のURLを入力してください","ChocoMint","http://"
	bbsurl=refstr
	if bbsurl="":return
	notesel bbs
	noteload "bbs.txt"
	bbs=bbs+bbsname+","+bbsurl+"\n"
	notesel bbs
	notesave "bbs.txt"
	exist "bbs.txt"
	if strsize=-1:notesel temp:notesave "bbs.txt"
	notesel bbs
	noteload "bbs.txt"
	repeat notemax
	noteget temp,cnt
	split temp,",",temp2
	lists+=temp2(0)
	loop
	objprm bbsmenus,lists
	dialog "登録しました"
}
if cmd == 3:exec dir_cur+"\\bbs.txt",16
if cmd == 4:exec dir_cur+"\\kakikomi.log",16
if cmd == 5:gosub *subjectload
if cmd == 8:gosub *threadload
return

*resize
	if (wParam=0)|(wParam=2) {
		cls
		color 255,255,255
		boxf
		color
		// コントロール作成
		objsize 120
		pos 0,0
		listbox select, ginfo_winy-24, lists
		bbsmenus=stat
		hListbox = objinfo(bbsmenus, 2)
		sendmsg hListbox,$197,notemax ;LB_SETTOPINDEX

		url=""
		pos 120,0
		input url,ginfo_winx-(120+80),20
		pos ginfo_winx-80,0
		objsize 80,20
		button "→",*urlrun
		
		objsize ginfo_winx-120,0.1953125*ginfo_winy
		pos 120,20
		listbox select2, 0.1953125*ginfo_winy, subject
		subjects=stat
		hListbox2 = objinfo(subjects, 2)

		sendmsg hStatus, WM_SIZE, 0, 0
		pos 120,0.5859375*ginfo_winy+0.06510416666*ginfo_winy
		mes "名前：",1:input name,130,19
		pos 300,0.5859375*ginfo_winy+0.06510416666*ginfo_winy
		mes "E-mail（省略可）：",1:input mail,130,19
		pos 575,0.5859375*ginfo_winy+30
		objsize 60,40
		button gosub "書き込み",*submit
		pos 120,ginfo_cy+30
		mesbox message,ginfo_winx-120,ginfo_winy-ginfo_cy-30,1
			
		WebView2_Size pCtrl, 120, 170, ginfo_winx,0.5859375*ginfo_winy
	}
return 0

*submit
if urlrunv=0{
notesel bbs
noteget temp,select
split temp,",",temp2
}
tm=temp2(1)
split tm,"/",board
bl=length(board)
dat=temps2(0)
strrep dat,".dat",""
//board(bl-2),dat
postdata="bbs="+board(bl-2)+"&key="+dat+"&FROM="+name+"&mail="+mail+"&MESSAGE="+message
netagent "Monazilla/1.0 ChocoMint/0.1"
neturl board(0)+"//"+board(2)+"/test/"
netrequest_post "bbs.cgi",postdata
;	結果待ちのためのループ
repeat
netexec res
if res : break
await
loop
netgetv kakikomik
dialog kakikomik,1,"書き込み確認(もう一度書き込みをクリック！)"
exist "kakikomi.log"
if strsize != -1{
notesel kakikomilog
noteload "kakikomi.log"
}else{
notesel kakikomiogg
notesave "kakikomi.log"
}
doweek="日","月","火","水","木","金","土"
kakikomilog += "time="+gettime(0)+"/"+gettime(1)+"/"+gettime(3)+"("+doweek(gettime(2))+")"+gettime(4)+":"+gettime(5)+":"+gettime(6)+"."+gettime(7)+"&"+postdata
notesave "kakikomi.log"
gosub *threadload
gosub *subjectload
return

*subjectload
selbbs=select
oldms = timegettime()
netagent "Monazilla/1.0 ChocoMint/0.1"
notesel bbs
noteget temp,select
split temp,",",temp2
neturl temp2(1)
netrequest_get "subject.txt"
;	結果待ちのためのループ
repeat
netexec res
if res : break
await
loop
netgetv subjecttxt
notesel subjecttxt
strrep subjecttxt, strf("%c", 10), "\n" //LFをCR+LFに
subject=""
repeat notemax
noteget temps,cnt
split temps,"<>",temps2
subject+=temps2(1)+"\n"
loop
objprm subjects,subject
sendmsg hListbox2,$197,notemax ;LB_SETTOPINDEX
sendmsg hStatus, $401, , "スレッド一覧をロードしました("+(timegettime()-oldms)+"ms)"
return

*threadload
oldms = timegettime()
if urlrunv=0{
netagent "Monazilla/1.0 ChocoMint/0.1"
notesel subjecttxt
noteget temps,select2
split temps,"<>",temps2
neturl temp2(1)+"dat/"
netrequest_get temps2(0)
;	結果待ちのためのループ
repeat
netexec res
if res : break
await
loop
netgetv Thread
}
notesel Thread
strrep Thread, strf("%c", 10), "\n" //LFをCR+LFに
noteget tempt,0
split tempt,"<>",tempt2
ThreadHTML={"<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">
<head>
<meta http-equiv="Content-Type" content="text/html;charset=Shift-JIS">
<meta http-equiv="Content-Style-Type" content="text/css">

<title>"}+tempt2(1)+{"</title>
</head>
<body vlink="#AA0088" text="#000000" link="#0000FF" bgcolor="#EFEFEF" alink="#FF0000">
<hr style="background-color:#888;color:#888;border-width:0;height:1px;position:relative;top:-.4em;">
<h1 style="color:#FF0000;font-size:larger;font-weight:normal;margin:-.5em 0 0;">"}+tempt2(4)+{"</h1>
<dl class="thread">"}
count=0
repeat notemax
count++
noteget tempt,cnt
split tempt,"<>",tempt2
if tempt2(1)=="":mailto="<font color=\"green\"><b>"+tempt2(0)+"</b></font>":else:mailto="<a href=\"mailto:"+tempt2(1)+"\"><font color=\"green\"><b>"+tempt2(0)+"</b></font></a>"
message_rep = replace(tempt2(3),"(http|https)://(.*)(?<!(png|gif|bmp|webp|jpg|jpeg|psd|tif|tiff|tga))","<a href=\"$1://$2/\">$1://$2</a>")
message_rep = replace(message_rep,"(http|https)://(.*).(png|gif|bmp|webp|jpg|jpeg|psd|tif|tiff|tga)","<a href=\"$1://$2.$3\" target=\"_blank\" title=\"$1://$2.$3\"><img src=\"$1://$2.$3\" alt=\"$1://$2.$3\" width=\"100\" height=\"100\"></img></a>")
message_rep = replace(message_rep,"sssp://(.*).gif","<img src=\"https://$1.gif\" alt=\"https://$1.gif\" title=\"https://$1.gif\"></img>")
ThreadHTML+="<dt>"+count+"："+mailto+" ： "+tempt2(2)+"</dt><dd> "+message_rep+" <br><br></dd>\n"
loop
ThreadHTML+="<hr>"
notesel ThreadHTML
notesave "Thread.html"
WebView2_Navigate pView, dir_cur+"\\Thread.html"
sendmsg hStatus, $401, , "スレッドをロードしました("+(timegettime()-oldms)+"ms)"
return

*urlrun
matches kekkau,url,"(http|https)://(.*)/test/read.cgi/(.*)/(.*)/"
sureurl = kekkau(0,1)+"//"+kekkau(0,2)+"/"+kekkau(0,3)+"/dat/"
temp2(1) = kekkau(0,1)+"//"+kekkau(0,2)+"/"+kekkau(0,3)+"/"
temps2(0) = kekkau(0,4)+".dat"
netagent "Monazilla/1.0 ChocoMint/0.1"
neturl sureurl
netrequest_get kekkau(0,4)+".dat"
;	結果待ちのためのループ
repeat
netexec res
if res : break
await
loop
Thread=""
netgetv Thread
urlrunv=1
gosub *threadload
stop

*ONEXIT_
if wparam=0{
	WebView2_Release pView
	WebView2_Release pCtrl
	WebView2_Release pEnv
	exist "Thread.html"
	if strsize!=-1:delete "Thread.html"
	end
}else{
return
}